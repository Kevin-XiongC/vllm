// SPDX-License-Identifier: Apache-2.0
// SPDX-FileCopyrightText: Copyright contributors to the vLLM project
//
// Kimi K2 MoE fused gate kernel, adapted from SGLang:
// https://github.com/sgl-project/sglang

#include <c10/cuda/CUDAException.h>
#include <c10/cuda/CUDAStream.h>
#include <cuda_bf16.h>
#include <cuda_runtime.h>
#include <torch/all.h>

#include <cfloat>
#include <type_traits>

namespace {

static constexpr int WARP_SIZE = 32;
static constexpr int WARPS_PER_CTA = 6;
static constexpr int NUM_EXPERTS = 384;
static constexpr int VPT = 12;  // 384 / 32 = 12

static constexpr int SMALL_TOKEN_THRESHOLD = 512;
static constexpr int WARPS_PER_TOKEN_SMALL = 12;
static constexpr int THREADS_PER_BLOCK_SMALL =
    WARPS_PER_TOKEN_SMALL * WARP_SIZE;

static constexpr int VEC_SIZE = 4;
static constexpr int MAX_TOPK = 8;

template <typename T>
__device__ __forceinline__ float to_float(T value) {
  if constexpr (std::is_same_v<T, __nv_bfloat16>) {
    return __bfloat162float(value);
  } else {
    return static_cast<float>(value);
  }
}

template <typename InputT>
__global__ void kimi_k2_moe_fused_gate_kernel_small_token(
    const InputT* input, const float* bias, float* output_ptr,
    int32_t* indices_ptr, int64_t num_rows, int64_t topk, bool renormalize,
    double routed_scaling_factor, bool apply_routed_scaling_factor_on_output) {
  int64_t row_idx = blockIdx.x;
  if (row_idx >= num_rows) {
    return;
  }

  int tid = threadIdx.x;
  int warp_id = tid / WARP_SIZE;
  int lane_id = tid % WARP_SIZE;

  __shared__ float shared_scores[NUM_EXPERTS];
  __shared__ float shared_original_scores[NUM_EXPERTS];
  __shared__ int selected_experts[MAX_TOPK];
  __shared__ float warp_maxs[WARPS_PER_TOKEN_SMALL];
  __shared__ int warp_experts[WARPS_PER_TOKEN_SMALL];

  if (tid < NUM_EXPERTS) {
    float input_val = to_float(input[row_idx * NUM_EXPERTS + tid]);
    float bias_val = bias[tid];
    float sigmoid_val = 1.0f / (1.0f + expf(-input_val));
    float biased_val = sigmoid_val + bias_val;
    shared_scores[tid] = biased_val;
    shared_original_scores[tid] = sigmoid_val;
  }

  __syncthreads();

  for (int k = 0; k < topk; k++) {
    float my_val = (tid < NUM_EXPERTS) ? shared_scores[tid] : -FLT_MAX;
    int my_expert = tid;

    float warp_max_val = my_val;
    int warp_max_expert = my_expert;

#pragma unroll
    for (int offset = 16; offset > 0; offset /= 2) {
      float other_val = __shfl_down_sync(0xFFFFFFFF, warp_max_val, offset);
      int other_expert = __shfl_down_sync(0xFFFFFFFF, warp_max_expert, offset);
      if (other_val > warp_max_val) {
        warp_max_val = other_val;
        warp_max_expert = other_expert;
      }
    }

    if (lane_id == 0) {
      warp_maxs[warp_id] = warp_max_val;
      warp_experts[warp_id] = warp_max_expert;
    }

    __syncthreads();

    if (warp_id == 0) {
      float final_max =
          (lane_id < WARPS_PER_TOKEN_SMALL) ? warp_maxs[lane_id] : -FLT_MAX;
      int final_expert =
          (lane_id < WARPS_PER_TOKEN_SMALL) ? warp_experts[lane_id] : -1;

#pragma unroll
      for (int offset = 16; offset > 0; offset /= 2) {
        float other_val = __shfl_down_sync(0xFFFFFFFF, final_max, offset);
        int other_expert = __shfl_down_sync(0xFFFFFFFF, final_expert, offset);
        if (other_val > final_max) {
          final_max = other_val;
          final_expert = other_expert;
        }
      }

      if (lane_id == 0) {
        selected_experts[k] = final_expert;
      }
    }

    __syncthreads();

    int selected = selected_experts[k];
    if (tid == selected) {
      shared_scores[tid] = -FLT_MAX;
    }

    __syncthreads();
  }

  if (tid == 0) {
    for (int k = 0; k < topk; k++) {
      int expert_id = selected_experts[k];
      if (expert_id >= 0 && expert_id < NUM_EXPERTS) {
        output_ptr[row_idx * topk + k] = shared_original_scores[expert_id];
        indices_ptr[row_idx * topk + k] = expert_id;
      } else {
        output_ptr[row_idx * topk + k] = 0.0f;
        indices_ptr[row_idx * topk + k] = 0;
      }
    }

    if (renormalize) {
      float sum = 0.0f;
      for (int k = 0; k < topk; k++) {
        sum += output_ptr[row_idx * topk + k];
      }
      if (sum > 0.0f) {
        for (int k = 0; k < topk; k++) {
          int64_t idx = row_idx * topk + k;
          output_ptr[idx] /= sum;
          if (apply_routed_scaling_factor_on_output) {
            output_ptr[idx] *= static_cast<float>(routed_scaling_factor);
          }
        }
      }
    }
  }
}

template <typename InputT>
__global__ void kimi_k2_moe_fused_gate_kernel(
    const InputT* input, const float* bias, float* output_ptr,
    int32_t* indices_ptr, int64_t num_rows, int64_t topk, bool renormalize,
    double routed_scaling_factor, bool apply_routed_scaling_factor_on_output) {
  int64_t row_idx = blockIdx.x * WARPS_PER_CTA + threadIdx.y;
  if (row_idx >= num_rows) {
    return;
  }

  int lane_id = threadIdx.x;
  int warp_id = threadIdx.y;

  __shared__ float shared_scores[NUM_EXPERTS * WARPS_PER_CTA];
  __shared__ float shared_original_scores[NUM_EXPERTS * WARPS_PER_CTA];

  float* warp_scores = shared_scores + warp_id * NUM_EXPERTS;
  float* warp_original_scores = shared_original_scores + warp_id * NUM_EXPERTS;

  if constexpr (std::is_same_v<InputT, float>) {
    constexpr int VEC_PER_LANE = VPT / VEC_SIZE;
    const float4* input_vec =
        reinterpret_cast<const float4*>(input + row_idx * NUM_EXPERTS);
    const float4* bias_vec = reinterpret_cast<const float4*>(bias);

#pragma unroll
    for (int i = 0; i < VEC_PER_LANE; i++) {
      int vec_idx = lane_id * VEC_PER_LANE + i;
      float4 input_val = input_vec[vec_idx];
      float4 bias_val = bias_vec[vec_idx];

#pragma unroll
      for (int j = 0; j < VEC_SIZE; j++) {
        int expert = vec_idx * VEC_SIZE + j;
        float inp = reinterpret_cast<float*>(&input_val)[j];
        float b = reinterpret_cast<float*>(&bias_val)[j];
        float sigmoid_val = 1.0f / (1.0f + expf(-inp));
        warp_scores[expert] = sigmoid_val + b;
        warp_original_scores[expert] = sigmoid_val;
      }
    }
  } else {
#pragma unroll
    for (int expert = lane_id; expert < NUM_EXPERTS; expert += WARP_SIZE) {
      float inp = to_float(input[row_idx * NUM_EXPERTS + expert]);
      float b = bias[expert];
      float sigmoid_val = 1.0f / (1.0f + expf(-inp));
      warp_scores[expert] = sigmoid_val + b;
      warp_original_scores[expert] = sigmoid_val;
    }
  }

  __syncthreads();

  for (int k = 0; k < topk; k++) {
    float max_val = -FLT_MAX;
    int max_expert = -1;

    for (int expert = lane_id; expert < NUM_EXPERTS; expert += WARP_SIZE) {
      if (warp_scores[expert] > max_val) {
        max_val = warp_scores[expert];
        max_expert = expert;
      }
    }

    for (int offset = WARP_SIZE / 2; offset > 0; offset /= 2) {
      float other_val = __shfl_down_sync(0xFFFFFFFF, max_val, offset);
      int other_expert = __shfl_down_sync(0xFFFFFFFF, max_expert, offset);
      if (other_val > max_val ||
          (other_val == max_val && other_expert < max_expert)) {
        max_val = other_val;
        max_expert = other_expert;
      }
    }

    if (lane_id == 0) {
      int64_t output_idx = row_idx * topk + k;
      if (max_expert != -1) {
        output_ptr[output_idx] = warp_original_scores[max_expert];
        indices_ptr[output_idx] = max_expert;
        warp_scores[max_expert] = -FLT_MAX;
      } else {
        output_ptr[output_idx] = 0.0f;
        indices_ptr[output_idx] = 0;
      }
    }

    __syncwarp();
  }

  __syncthreads();

  if (renormalize && lane_id == 0) {
    float sum = 0.0f;
    for (int k = 0; k < topk; k++) {
      sum += output_ptr[row_idx * topk + k];
    }
    if (sum > 0.0f) {
      for (int k = 0; k < topk; k++) {
        int64_t idx = row_idx * topk + k;
        output_ptr[idx] /= sum;
        if (apply_routed_scaling_factor_on_output) {
          output_ptr[idx] *= static_cast<float>(routed_scaling_factor);
        }
      }
    }
  }
}

}  // namespace

template <typename InputT>
void launch_kimi_k2_moe_fused_gate(const InputT* input_ptr,
                                   const float* bias_ptr, float* output_ptr,
                                   int32_t* indices_ptr, int64_t num_rows,
                                   int64_t topk, bool renormalize,
                                   double routed_scaling_factor,
                                   bool apply_routed_scaling_factor_on_output,
                                   cudaStream_t stream) {
  if (num_rows <= SMALL_TOKEN_THRESHOLD) {
    kimi_k2_moe_fused_gate_kernel_small_token<<<
        num_rows, THREADS_PER_BLOCK_SMALL, 0, stream>>>(
        input_ptr, bias_ptr, output_ptr, indices_ptr, num_rows, topk,
        renormalize, routed_scaling_factor,
        apply_routed_scaling_factor_on_output);
  } else {
    int64_t num_blocks = (num_rows + WARPS_PER_CTA - 1) / WARPS_PER_CTA;
    dim3 block_dim(WARP_SIZE, WARPS_PER_CTA);
    kimi_k2_moe_fused_gate_kernel<<<num_blocks, block_dim, 0, stream>>>(
        input_ptr, bias_ptr, output_ptr, indices_ptr, num_rows, topk,
        renormalize, routed_scaling_factor,
        apply_routed_scaling_factor_on_output);
  }
}

std::tuple<torch::Tensor, torch::Tensor> kimi_k2_moe_fused_gate(
    const torch::Tensor& input, const torch::Tensor& bias, int64_t topk,
    bool renormalize, double routed_scaling_factor,
    bool apply_routed_scaling_factor_on_output) {
  TORCH_CHECK(input.is_cuda(), "input must be a CUDA tensor");
  TORCH_CHECK(bias.is_cuda(), "bias must be a CUDA tensor");
  TORCH_CHECK(input.is_contiguous(), "input must be contiguous");
  TORCH_CHECK(bias.is_contiguous(), "bias must be contiguous");
  TORCH_CHECK(input.dim() == 2, "input must be a 2D tensor");
  TORCH_CHECK(bias.dim() == 1, "bias must be a 1D tensor");
  TORCH_CHECK(
      input.scalar_type() == at::kFloat || input.scalar_type() == at::kBFloat16,
      "input must be float32 or bfloat16");
  TORCH_CHECK(bias.scalar_type() == at::kFloat, "bias must be float32");
  TORCH_CHECK(topk > 0 && topk <= MAX_TOPK,
              "kimi_k2_moe_fused_gate only supports 1 <= topk <= ", MAX_TOPK,
              ", got ", topk);

  int64_t num_rows = input.size(0);
  int64_t num_experts = input.size(1);
  TORCH_CHECK(num_experts == NUM_EXPERTS,
              "kimi_k2_moe_fused_gate only supports ", NUM_EXPERTS,
              " experts, got ", num_experts);
  TORCH_CHECK(bias.numel() == NUM_EXPERTS, "bias must contain ", NUM_EXPERTS,
              " elements");

  auto output =
      torch::empty({num_rows, topk}, input.options().dtype(torch::kFloat32));
  auto indices =
      torch::empty({num_rows, topk}, input.options().dtype(torch::kInt32));

  auto stream = c10::cuda::getCurrentCUDAStream(input.get_device()).stream();

  if (input.scalar_type() == at::kFloat) {
    launch_kimi_k2_moe_fused_gate(
        input.data_ptr<float>(), bias.data_ptr<float>(),
        output.data_ptr<float>(), indices.data_ptr<int32_t>(), num_rows, topk,
        renormalize, routed_scaling_factor,
        apply_routed_scaling_factor_on_output, stream);
  } else {
    launch_kimi_k2_moe_fused_gate(
        reinterpret_cast<__nv_bfloat16 const*>(input.data_ptr()),
        bias.data_ptr<float>(), output.data_ptr<float>(),
        indices.data_ptr<int32_t>(), num_rows, topk, renormalize,
        routed_scaling_factor, apply_routed_scaling_factor_on_output, stream);
  }

  C10_CUDA_KERNEL_LAUNCH_CHECK();

  return std::make_tuple(output, indices);
}
